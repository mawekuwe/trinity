# $Id$
# Maintainer: Baho Utot <baho-utot@columbus.rr.com>
pkgname=${_module}dbus-tqt
pkgver=3.5.13
pkgrel=00
arch=(i686 x86_64)
url="http://trinity.pearsoncomputing.net"
license=(GPL2)
groups=(trinity)
pkgdesc="Trinity - dbus-tqt"
depends=(dbus-core tqtinterface)
makedepends=(cmake)
options=(!docs !emptydirs)
source=(${pkgname}-${pkgver}.tar.xz)

_prefix=/usr

mksource() {
	git clone http://scm.trinitydesktop.org/scm/git/${pkgname}
	pushd ${pkgname}
		git submodule init
		git submodule update
		git checkout v3.5.13
	popd
	tar -cvJf ${pkgname}-${pkgver}.tar.xz ${pkgname}/*
	rm -fr ${pkgname}
}


build() {
	msg "Building revision $pkgver"
	mkdir -vp ${srcdir}/BUILD
	cd ${srcdir}/BUILD
	msg "Starting cmake..."
	cmake ${srcdir}/dbus-tqt/ \
		-DCMAKE_INSTALL_PREFIX=${_prefix} \
		-DCMAKE_SKIP_RPATH=ON
	make
}

package() {
	msg "Packaging - $pkgname-$pkgver"
	cd ${srcdir}/BUILD
	make DESTDIR="$pkgdir/" install
}
md5sums=('46ae165c068271485c827f6fc6687a9a')
