# Maintainer: Baho Utot <baho-utot@columbus.rr.com>
pkgname=kdelibs
pkgver=3.5.13
pkgrel=00
arch=('i686' 'x86_64')
url="http://trinity.pearsoncomputing.net"
license=('GPL2')
groups=('trinity')
pkgdesc="Trinity - ${pkgname}"
depends=(libart-lgpl jasper openexr aspell libtiff
	alsa-lib arts libcups libxcomposite libxslt
	libxml2 hicolor-icon-theme java-environment )
makedepends=(cmake)
optdepends=('pmount: mount removable devices as normal user')
options=(!emptydirs)
install='kdelibs.install'
source=(${pkgname}-${pkgver}.tar.xz)
source=(${pkgname}-${pkgver}.tar.gz)

_prefix=/usr
mksource() {
	git clone http://scm.trinitydesktop.org/scm/git/${pkgname}
	pushd ${pkgname}
		git submodule init
		git submodule update
		rm -rf libltdl
		git checkout v3.5.13
	popd
	tar -cvJf ${pkgname}-${pkgver}.tar.xz ${pkgname}/*
	rm -fr ${pkgname}
}
_trinity.sh() {
	#	Build trinity.sh
	install -d -m755 ${pkgdir}/etc/profile.d/
	echo "export KDEDIR=${_prefix}" > ${pkgdir}/etc/profile.d/trinity.sh
	echo "export KDEDIRS=${_prefix}" >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'PATH=${KDEDIR}/bin:${PATH:=}' >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'PATH=${PATH%:}' >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'XDG_DATA_DIRS=${KDEDIR}/share:${XDG_DATA_DIRS:=}' >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'XDG_DATA_DIRS=${XDG_DATA_DIRS%:}' >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'XDG_CONFIG_DIRS=${KDEDIR}/etc/xdg:${XDG_CONFIG_DIRS:=}' >> ${pkgdir}/etc/profile.d/trinity.sh
	echo 'XDG_CONFIG_DIRS=${XDG_CONFIG_DIRS%:}' >> ${pkgdir}/etc/profile.d/trinity.sh
}
build() {
	msg "Building revision $pkgver"
	export KDECONFIG=kde-config
	mkdir -vp ${srcdir}/BUILD
	cd ${srcdir}/BUILD
	msg "Starting cmake..."
	cmake ${srcdir}/${pkgname}/ \
		-DCMAKE_INSTALL_PREFIX=${_prefix} \
		-DSYSCONF_INSTALL_DIR=/etc \
		-DWITH_LIBART=ON \
		-DWITH_TIFF=ON \
		-DWITH_JASPER=ON \
		-DWITH_OPENEXR=ON \
		-DWITH_ASPELL=ON \
		-DWITH_UTEMPTER=OFF \
		-DBUILD_ALL=ON
	make
}
package() {
	msg "Packaging - $pkgname-$pkgver"
	cd ${srcdir}/BUILD
	make DESTDIR="$pkgdir/" install
	#	Build linker file
	install -d -m755 ${pkgdir}/etc/ld.so.conf.d/
	echo "${_prefix}/lib" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
	_trinity.sh
	rm -rf ${pkgdir}${_prefix}/share/icons/hicolor/index.theme
	rm -rf ${pkgdir}${_prefix}/share/doc/kde/HTML/en/kspell/common
}
md5sums=('f62eefb63b4ba2141b4c576f859806dd')
